! LINGO solver;
! developer: Tatiana Balbi Fraga;
! data: 2023/08/09
! last modification: 2023/08/23

MODEL:
! Multi-product batch processing time maximization problem;

SETS:

PRODUCTS:	PRODUCTION_RATE,
	DEMAND,
	FREE_OUTLET_INVENTORY,
	FREE_FACTORY_INVENTORY,
	PRODUCTION,
	DELIVERED,
	SEND_TO_OUTLET,
	STOCK_IN_FACTORY;

PAIR;

COMP(PRODUCTS, PAIR): PRIOR_DEMAND;

TREO;

COMP2(PRODUCTS, TREO): PRIOR_OUTLETS;

ENDSETS

DATA:

PRODUCTS = @FILE('data.ldt');
PRODUCTION_RATE = @FILE('data.ldt');
DEMAND = @FILE('data.ldt');
TOTAL_FREE_OUTLET_INVENTORY = @FILE('data.ldt');
FREE_OUTLET_INVENTORY = @FILE('data.ldt');
TOTAL_FREE_FACTORY_INVENTORY = @FILE('data.ldt');
FREE_FACTORY_INVENTORY = @FILE('data.ldt');
PROCESSING_TIME_LIMIT = @FILE('data.ldt');

PAIR = 1..2;
TREO = 1..3;

ENDDATA

MAX = BATCH_PROCESSING_TIME;

!BATCH_PROCESSING_TIME = 40;

@FOR( PRODUCTS(P): [CALC_PROD_1] PRODUCTION(p) - PRODUCTION_RATE(p)*BATCH_PROCESSING_TIME = 0 ); 

@FOR( PRODUCTS(P): [CALC_PROD_2] PRODUCTION(p) - DELIVERED(p) - SEND_TO_OUTLET(p) - STOCK_IN_FACTORY(p) = 0 );

@FOR( PRODUCTS(P): [REST_DELIVERED] DELIVERED(p) <= DEMAND(P) );

@FOR( PRODUCTS(P): [REST_OUTLET] SEND_TO_OUTLET(p) <= FREE_OUTLET_INVENTORY(P) );

[REST_TOTAL_OUTLETS] @SUM( PRODUCTS(P): SEND_TO_OUTLET(p) ) <= TOTAL_FREE_OUTLET_INVENTORY;

@FOR( PRODUCTS(P): [REST_FACTORY] STOCK_IN_FACTORY(p) <= FREE_FACTORY_INVENTORY(P) );

[REST_TOTAL_FACTORY] @SUM( PRODUCTS(P): STOCK_IN_FACTORY(p) ) <= TOTAL_FREE_FACTORY_INVENTORY;

[REST_PROCESSING_TIME] BATCH_PROCESSING_TIME <= PROCESSING_TIME_LIMIT;

@FOR( PRODUCTS(p): PRIOR_DEMAND(p,1) <= DEMAND(p) - DELIVERED(p) + 0.5);
@FOR( PRODUCTS(p): PRIOR_DEMAND(p,2) <= STOCK_IN_FACTORY(p) + SEND_TO_OUTLET(p) + 0.5);

@FOR( PRODUCTS(p): @MIN(PAIR(i): PRIOR_DEMAND(p,i)) = 0);

@FOR( PRODUCTS(p): PRIOR_OUTLETS(p,1) <= TOTAL_FREE_OUTLET_INVENTORY - @SUM(PRODUCTS(p): SEND_TO_OUTLET(p)) + 0.5);
@FOR( PRODUCTS(p): PRIOR_OUTLETS(p,2) <= FREE_OUTLET_INVENTORY(p) - SEND_TO_OUTLET(p) + 0.5);
@FOR( PRODUCTS(p): PRIOR_OUTLETS(p,3) <= STOCK_IN_FACTORY(p) + 0.5);

@FOR( PRODUCTS(p): @MIN(TREO(i): PRIOR_OUTLETS(p,i)) = 0);

@GIN(BATCH_PROCESSING_TIME);
@FOR( PRODUCTS(p):@GIN(DELIVERED(p)) );
@FOR( PRODUCTS(p):@GIN(STOCK_IN_FACTORY(p)) );
@FOR( PRODUCTS(p):@GIN(SEND_TO_OUTLET(p)) );