! LINGO solver developed for the problem of elaborating water distribution schedules in periods of scarcity;
! developer: Tatiana Balbi Fraga;


MODEL:
! Problem of Elaborating Water Distribution Schedules in Periods of Scarcity;
! New tests;


SETS:

DAYS;

SHIFTS;

LOCALS: 	IDEAL_INFLOW_RATE, 
	  	MAX_INFLOW_RATE, 
	  	MIN_INFLOW_RATE, 
		IDEAL_INFLOW, 
		INITIAL_VOLUME, 
		CAPACITY, 
		RELEVANCE_FACTOR;

DAILY_SHIFTS (DAYS, SHIFTS): C3;

SOLUTION (DAYS, SHIFTS, LOCALS): 	VALVE_STATUS, 
						INFLOW, 
						OUTFLOW, 
						RES_VOLUME, 
						IDLE_CAPACITY;

SOLUTION_DAYAND (SOLUTION)| &1 #EQ# N_DAYS #AND# &2 #EQ# N_SHIFTS: IDLE_CAP_EXP_WEIG;

RESERVOIRS(LOCALS) | &1 #LE# N_RESERVOIRS;

ZONES(LOCALS) | &1 #GT# N_RESERVOIRS: 	HOUSEHOLDS, 
				DEMAND, 
				ZONE_AVERAGE_CAPACITY, 
				ZONE_OUTFLOW, 
				DIFF, 
				DEMAND_PER,
				ZONE_OUTFLOW_PER;

DISTRIBUTION(RESERVOIRS, LOCALS): NET;

SOLUTION_RESERVOIRS (DAYS, SHIFTS, RESERVOIRS);

SOLUTION_ZONES (DAYS, SHIFTS, ZONES);

ENDSETS

DATA:

DAYS = @FILE('data.ldt');
SHIFTS = @FILE('data.ldt');
LOCALS = @FILE('data.ldt');
N_RESERVOIRS = @FILE('data.ldt');
HOUSING_COEFF = @FILE('data.ldt');
DAILY_COMSUMPTION = @FILE('data.ldt');
TIME_PER_SHIFT = @FILE('data.ldt');
HOUSEHOLDS = @FILE('data.ldt');
NET = @FILE('data.ldt');
MIN_INFLOW_RATE = @FILE('data.ldt');
MAX_INFLOW_RATE = @FILE('data.ldt');
ZONE_AVERAGE_CAPACITY = @FILE('data.ldt');
CAPACITY = @FILE('data.ldt');

C2 = 100000;
RELEVANCE_FACTOR = 1.13 1.13 1.12 1.12 1.12 1.12 1.0 1.0 1.0 1.0 1.0 1.0;


ENDDATA

CALC:

N_DAYS = @SIZE(DAYS);
N_SHIFTS = @SIZE(SHIFTS);

N_TOTAL_HOUSEHOLDS = @SUM( ZONES(z): HOUSEHOLDS(z) );

@FOR( ZONES(z): DEMAND(z) = HOUSEHOLDS(z) * HOUSING_COEFF * DAILY_COMSUMPTION * N_DAYS );

TOTAL_DEMAND = @SUM( ZONES(z): DEMAND(z) );

@FOR( ZONES(z): [CALC_DP] DEMAND_PER(z) = DEMAND(z)/TOTAL_DEMAND); 

@FOR( ZONES(z): CAPACITY(z) = HOUSEHOLDS(z) * ZONE_AVERAGE_CAPACITY(z));

@FOR (LOCALS(l): INITIAL_VOLUME(l) = 0);

MAX_INFLOW_RATE_INF = @MAX(LOCALS: MAX_INFLOW_RATE);

@FOR( LOCALS (l):
	@IFC( MAX_INFLOW_RATE (l) #LE# 1.2345678: 
		MAX_INFLOW_RATE (l) = MAX_INFLOW_RATE_INF ) );

@FOR( LOCALS (l):
	@IFC( MIN_INFLOW_RATE (l) #LE# 1.2345678: 
		MIN_INFLOW_RATE(l) = 0.00 ) );

C1 = 10/N_DAYS;

!@FOR (DAILY_SHIFTS(d,s) : 
	c(d,s) = 1 );

@FOR (DAILY_SHIFTS(d,s) : 
	C3(d,s) = 3*((N_SHIFTS-s)*N_DAYS + N_DAYS - d + 1) );

CAP_EXP_WEIGHTED_SUM = @SUM( LOCALS(l) : CAPACITY(l)^RELEVANCE_FACTOR(l)) ;

C4 = 1 / CAP_EXP_WEIGHTED_SUM;

ENDCALC

! The objective;

[Eq01] MAX = FirstTerm - SecondTerm - ThirdTerm - FourthTerm;

!first term - average gain of distributed volume;
!in the optimal solution, this term should not vary according to the number of days;
!normally, in the optimal solution, the value of this term will be as high as possible;

FirstTerm = C1*TOTAL_ZONE_OUTFLOW;

!second term - cost of inequality in distribution; 
!in the solution, the second term varies between 0 and C2; 
!to force a fair distribution, the parameters of the objective function must be defined 
 so that the second term has a significatife weight;
!normally we will have SecondTerm = 0 in the optimal solution;

SecondTerm = C2*@SUM( ZONES(z): DIFF(z) );

!third term - cost of closing the valves;
!normally in the optimal solution, Third Term=0;
!except when flow restrictions force valves to close to ensure fair distribution; 
!in the latter case, the lowest possible cost is expected;

ThirdTerm = @SUM( SOLUTION(d,s,l): C3(d,s)*(1-VALVE_STATUS(d,s,l)) );

!fourth term - cost of idle capacity in reservoirs;
!this term is used to prioritize or not the distribution of remaining water between the reservoirs;
!increasing the value of C4 will make the filling of the reservoirs prioritized to meet the demand;

FourthTerm = C4*IDLE_CAP_EXP_WEIGHTED_SUM;

!Restrictions;

! zones;

[Eq15] TOTAL_ZONE_OUTFLOW - @SUM(ZONES(z): ZONE_OUTFLOW(z) ) = 0;

@FOR ( ZONES(z): [Eq13]
	ZONE_OUTFLOW(z) - DEMAND(z) <= 0 );

@FOR( ZONES(z): [Eq14]
	ZONE_OUTFLOW(z) - @SUM( DAILY_SHIFTS(d,s): OUTFLOW(d,s,z) ) = 0 );

@FOR ( ZONES(z): [Eq01_2T]
	DIFF(z) - @ABS( DEMAND_PER(z) - ZONE_OUTFLOW_PER(z) ) = 0 );

!@FOR ( ZONES(z): [Eq01_2T]
	DIFF(z) - @SQR( DEMAND_PER(z) - ZONE_OUTFLOW_PER(z) ) = 0 );

!@FOR ( ZONES(z): [Eq01_2T]
	DIFF(z) - @POW( DEMAND_PER(z) - ZONE_OUTFLOW_PER(z), 2) = 0 );

@FOR( ZONES(z): [Eq16] ZONE_OUTFLOW_PER(z) - ZONE_OUTFLOW(z)/TOTAL_ZONE_OUTFLOW = 0);

! reservoirs;

@FOR ( SOLUTION_RESERVOIRS(d,s,r): [Eq06]
	OUTFLOW(d,s,r) - @SUM( LOCALS(l): INFLOW(d,s,l) * NET(r,l) ) = 0 );

![CALC_AVER_INFLOW] HOUSEHOLD_AVER_INFLOW - TOTAL_ZONE_OUTFLOW / (N_TOTAL_HOUSEHOLDS * N_DAYS) = 0;
![CALC_INHABIT_AVER_INFLOW] INHABIT_AVER_INFLOW - HOUSEHOLD_AVER_INFLOW / HOUSING_COEFF = 0;

! all locals;

@FOR( LOCALS(l): [Eq04]
	IDEAL_INFLOW_RATE(l) - MAX_INFLOW_RATE(l) <= 0 );

@FOR( LOCALS(l): [Eq05]
	IDEAL_INFLOW_RATE(l) - MIN_INFLOW_RATE(l) >= 0 );

@FOR( LOCALS(l): [Eq03]
	IDEAL_INFLOW(l) - IDEAL_INFLOW_RATE(l) * TIME_PER_SHIFT = 0 );

@FOR( SOLUTION(d,s,l): [Eq02]
	INFLOW(d,s,l) - IDEAL_INFLOW(l) * VALVE_STATUS(d,s,l) = 0 );

@FOR( SOLUTION(d,s,l): [Eq10]
	RES_VOLUME(d,s,l) >= 0  );

@FOR( SOLUTION(d,s,l): [Eq11_Eq12]
	RES_VOLUME(d,s,l) - CAPACITY(l) <= 0  );

@FOR ( SOLUTION(d,s,l) | d #EQ# 1 #AND# s #EQ# 1: [Eq07_09]
	RES_VOLUME(d,s,l) - INITIAL_VOLUME(l) - INFLOW(d,s,l) + OUTFLOW(d,s,l) = 0 );

@FOR ( SOLUTION(d,s,l) | d #NE# 1 #AND# s #EQ# 1: [Eq07]
	RES_VOLUME(d,s,l) - RES_VOLUME(d-1,N_SHIFTS,l) - INFLOW(d,s,l) + OUTFLOW(d,s,l) = 0 );

@FOR ( SOLUTION(d,s,l) | s #NE# 1: [Eq08]
	RES_VOLUME(d,s,l) - RES_VOLUME(d,s-1,l) - INFLOW(d,s,l) + OUTFLOW(d,s,l) = 0 );
 
@FOR( SOLUTION(d,s,l): [Eq17]
	IDLE_CAPACITY(d,s,l) - CAPACITY(l) + RES_VOLUME(d,s,l) = 0);

[Eq18] IDLE_CAP_EXP_WEIGHTED_SUM - @SUM( SOLUTION_DAYAND(d,s,l) : IDLE_CAP_EXP_WEIG(d,s,l) ) = 0;

@FOR( SOLUTION_DAYAND(d,s,l): [Eq18_2T] IDLE_CAP_EXP_WEIG(d,s,l) = IDLE_CAPACITY(d,s,l)^RELEVANCE_FACTOR(l) );

! nature of the variables;

@FOR( SOLUTION: [Eq19]
	@BIN( VALVE_STATUS) );

END